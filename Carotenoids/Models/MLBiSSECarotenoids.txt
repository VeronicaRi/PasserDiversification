#######################################
########## BiSSE Carotenoids ##########
#######################################

setOption("useScaling","true")
NUM_STATES = 2

### Read in the data

data = readCharacterDataDelimited(file="/home/alex/Desktop/Veronica/Data/pasercarReady.tsv", stateLabels=2, type="NaturalNumbers", delimiter="\t", headers=FALSE)

### Reading phylogeny
phylogeny <- readTrees(file="/home/alex/Desktop/Veronica/Data/frug50tree.nex")[1]

# Get some useful variables from the data. We need these later on.
taxa <- phylogeny.taxa()


# set my move index
H = 0.587405

####################
# Create the rates #
####################


### Create the constant prior parameters of the diversification rates
## Number of surviving lineages is 5983
rate_mean <- ln( ln(5983.0/2.0) / phylogeny.rootAge() )
rate_sd <- 2*H


for (i in 1:NUM_STATES) {

### Create a lognormal distributed variable for the diversification rate
log_speciation[i] ~ dnNormal(mean=rate_mean,sd=rate_sd)
log_speciation[i].setValue( rate_mean )
speciation[i] := exp( log_speciation[i] )
moves[++mvi] = mvSlide(log_speciation[i],delta=0.20,tune=true,weight=3.0)

### Create a lognormal distributed variable for the turnover rate
log_extinction[i] ~ dnNormal(mean=rate_mean,sd=rate_sd)
log_extinction[i].setValue( rate_mean )
extinction[i] := exp( log_extinction[i] )
moves[++mvi] = mvSlide(log_extinction[i],delta=0.20,tune=true,weight=3)

}
################################################
### Set up the transition rate matrix for observed states ###
################################################
## I defined very loosely my gamma priors for rates in the Q-mat

shape_pr <- 0.5
rate_pr := 1


rate_12 ~ dnGamma(shape=shape_pr, rate=rate_pr)
rate_21 ~ dnGamma(shape=shape_pr, rate=rate_pr)

moves[++mvi] = mvScale( rate_12, weight=2 )
moves[++mvi] = mvScale( rate_21, weight=2 )


#########################
### Create the rate matrix ###
########################

rate_matrix := fnFreeBinary( [rate_12, rate_21 ], rescaled=false)



########################################################
#        Set up the root state frequencies             #
### We're going to estimate them and not set them up ###
########################################################

rate_category_prior ~ dnDirichlet( rep(1,NUM_STATES) )
moves[++mvi] = mvDirichletSimplex(rate_category_prior,tune=true,weight=2)


### Create the fix parameter for the age of the root set to the observed age
root <- phylogeny.rootAge()

### rho is the probability of sampling species at the present
rho <- phylogeny.ntips()/5983


timetree ~ dnCDBDP( rootAge           = root,
speciationRates   = speciation,
extinctionRates   = extinction,
Q                 = rate_matrix,
pi                = rate_category_prior,
rho               = rho,
delta             = 1.0,
condition         = "time" )

### clamp the model with the "observed" tree
timetree.clamp( phylogeny )
timetree.clampCharData( data )

### workspace model wrapper ###
mymodel = model(rate_matrix)

monitors[++mni] = mnModel(filename="/home/alex/Desktop/Veronica/Data/output/ppBiSSEpassercar.log", printgen=1)
monitors[++mni] = mnScreen(printgen=10, rate_12, rate_21, speciation, extinction)

####################
### The Analysis ###
####################

power_pbisse = powerPosterior(mymodel, moves, monitors,"/home/alex/Desktop/Veronica/Data/output/ppBiSSEpassercar.out", cats=50)
power_pbisse.burnin(generations=500,tuningInterval=50)

power_pbisse.run(generations=1000)
